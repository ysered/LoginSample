apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

// keystore properties
def keystorePropertiesFile = rootProject.file("./properties/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    defaultConfig {
        applicationId "com.ysered.loginsample"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 2
        versionName "1.0"
        setProperty("archivesBaseName", "app-v${versionName}.${versionCode}")
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

ext {
    coroutinesVersion = '0.18'
    supportLibraryVersion = '26.1.0'
    constraintLayoutVersion = '1.0.2'
    archLifecycleVersion = '1.0.0-alpha9-1'
    facebookSdkVersion = '[4,5)'
    picassoVersion = '2.5.2'
    junitVersion = '4.12'
    espressoVersion = '2.2.2'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Support libraries
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"

    // Architecture component libraries
    implementation "android.arch.lifecycle:extensions:$archLifecycleVersion"
    kapt "android.arch.lifecycle:compiler:$archLifecycleVersion"

    // Facebook SDK
    implementation "com.facebook.android:facebook-android-sdk:$facebookSdkVersion"

    implementation "com.squareup.picasso:picasso:$picassoVersion"

    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
